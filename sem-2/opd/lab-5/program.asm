	ORG	0x244
START:	CLA
S1:	IN	0x07	; Спин-луп для чтения состояния готовности с ВУ-3
	AND	#0x40
	BEQ	S1	; Конец
	IN	0x06	; Прочитать первый символ
	CMP	#0x0D	; Проверка на стоп-символ, если он найден, то Z=1
	BEQ	SAVE	; При обнаружении стоп-символа не читаем второй символ	
	ST	TEMP	; Временно выгружаем первый символ (он перезапишется в спин-лупе чтения второго символа)
	CLA		; Очищаем аккумулятор для спин-лупа
S2:	IN	0x07	; Спин-луп для чтения состояния готовности с ВУ-3
	AND	#0x40
	BEQ	S2	; Конец
	IN	0x06	; Прочитать второй символ
	CMP	#0x0D	; Проверка на стоп-символ, если он найден, то Z=1
	PUSHF		; Сохраняем PS в стек, чтобы NZVC не были утеряны после ADD
	SWAB		; Второй символ в левый байт
	ADD	TEMP	; Возвращаем первый символ в правый байт
	POPF		; Возвращаем состояние NZVC перед командой ADD
SAVE:	ST	(ADDR)+	; Сохранить данные, переместив указатель на следующую ячейку
	BEQ	STOP	; Стоит Z=1 - значит, был стоп-символ, нужен останов
	JUMP	S1	; Продолжение чтения
STOP:	HLT

	ORG	0x270
TEMP:	WORD	?	; Ячейка для временного сохранения первого символа

	ORG	0x2A0
ADDR:	WORD	0x05AD	; Адрес сохранения символов

;СИМВ:	C	м	е	т	а	н	а	-	CR
;КОИ-8:	F3	CD	C5	D4	C1	CE	C1	2D	0D
;UTF-8: D0A1	D0BC	D0B5	D182	D0B0	D0BD	D0B0	2D	0D
;UTF-16:0421	043C	0435	0442	0430	043D	0430	002D	000D